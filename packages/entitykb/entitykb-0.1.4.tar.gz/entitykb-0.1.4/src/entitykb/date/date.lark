// rules

start:          is_prefix | is_date

is_prefix:      month_name (sep (num suffix? sep?)?)?   // Jan 1,
     |          num (sep (num sep?)?)?                  // 2019-01-
     |          num (sep (month_name sep?)?)?           // 2019-JAN-

is_date:        month_name sep num suffix? sep num      // Jan 1st, 2019
     |          num sep month_name sep num              // 2019-JAN-01
     |          num sep num sep num                     // 2019-01-03



// characters

sep:            /(-|\.| |\/|\\|,)+/
suffix:         /st|th|rd/

month_name:     JANUARY
          |     FEBRUARY
          |     MARCH
          |     APRIL
          |     MAY
          |     JUNE
          |     JULY
          |     AUGUST
          |     SEPTEMBER
          |     OCTOBER
          |     NOVEMBER
          |     DECEMBER

num:            DIGIT DIGIT? (DIGIT DIGIT)?

JANUARY:        /JAN(UARY)?/i
FEBRUARY:       /FEB(RUARY)?/i
MARCH:          /MAR(CH)?/i
APRIL:          /APR(IL)?/i
MAY:            /MAY/i
JUNE:           /JUN(E)?/i
JULY:           /JUL(Y)?/i
AUGUST:         /AUG(UST)?/i
SEPTEMBER:      /SEP((TEMBER|T))?/i
OCTOBER:        /OCT(OBER)?/i
NOVEMBER:       /NOV(EMBER)?/i
DECEMBER:       /DEC(EMBER)?/i

%import common.DIGIT -> DIGIT
%import common.WS
